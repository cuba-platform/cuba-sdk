def moduleName = "com.haulmont.cuba.cli.plugin.sdk.components.cuba"

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}
repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

task sourceJar(type: Jar) {
    from file('src/main/java')
    from file('src/main/kotlin')
    from file('src/main/resources')
    classifier = 'sources'
}

artifacts {
    archives sourceJar
}

dependencies {
    compile project(":cuba-sdk")
    implementation("com.github.kittinunf.fuel:fuel:$versions.fuel") {
        exclude(group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib')
    }
    implementation("org.apache.maven:maven-model:$versions.maven")
    configurations.compile.extendsFrom(configurations.extraLibs)
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${compileKotlin.destinationDir}"
        ]
        classpath = files()
    }
}

task bundle(group: 'distribution', dependsOn: ['jar', ':cuba-sdk:bundle'], type: Copy) {
    inputs.files jar.outputs.files

    def bundlePlatform = project.hasProperty('targetOsPlatform') ? '-' + project.property('targetOsPlatform') : '';

    def bundleOutput = file("${rootProject.buildDir}/bundle" + bundlePlatform)

    jar.outputs.files.each {
        from it
    }

    configurations.extraLibs.each {
        from it
    }

    def pluginsFolder = file("${bundleOutput}/plugins")
    pluginsFolder.mkdir()

    into pluginsFolder

    if (bundleOutput.exists()) {
        def osName = System.getProperty("os.name").toLowerCase()
        if (osName.contains('nux')
                || osName.contains('mac')) {
            exec {
                executable 'chmod'
                args = ['u+w', '-R', pluginsFolder]
            }
        }
    }

}

task generatePom(group: 'distribution', description: 'Generates POM file for uploading to repository') {
    doLast {
        def mavenPomDir = new File(project.buildDir, 'maven-pom')
        mavenPomDir.mkdirs()

        pom {
            project {
                name project.name
                description 'CUBA Platform Development SDK CUBA components plugin'
                inceptionYear '2018'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name 'CUBA Platform'
                    url 'https://www.cuba-platform.com'
                }
            }
        }.writeTo(new File(mavenPomDir, project.name + '-' + project.version + '.pom'))
    }
}

assemble.dependsOn(generatePom)
